buildscript {
    apply(from: {
        def searchDir = rootDir.toPath()
        do {
            def p = searchDir.resolve("node_modules/react-native-test-app/android/dependencies.gradle")
            if (p.toFile().exists()) {
                return p.toRealPath().toString()
            }
        } while (searchDir = searchDir.getParent())
        throw new GradleException("Could not find `react-native-test-app`");
    }())

    repositories {
        mavenCentral()
        google()
        maven { url "https://packages.rnrepo.org/releases" }
    }

    dependencies {
        getReactNativeDependencies().each { dependency ->
            classpath(dependency)
        }
        classpath("org.rnrepo.tools:prebuilds-plugin:0.1.0")
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url({
                def searchDir = rootDir.toPath()
                do {
                    def p = searchDir.resolve("node_modules/react-native/android")
                    if (p.toFile().exists()) {
                        return p.toRealPath().toString()
                    }
                } while (searchDir = searchDir.getParent())
                throw new GradleException("Could not find `react-native`");
            }())
        }
        mavenCentral()
        google()
        maven { url "https://packages.rnrepo.org/releases" }
    }
}

project(":app") {
    apply plugin: "org.rnrepo.tools.prebuilds-plugin"
}
